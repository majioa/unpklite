INIT_INT	PROC	NEAR
	;DS:SI-ADRESS
	PUSH	AX
	XOR	AX,AX
	PUSH	DS
	PUSH	AX
	POP	DS
	POP	AX
	CLI	
	XCHG	AX,DS:[1*4+2]
	XCHG	SI,DS:[1*4]
	STI
	MOV	DS,AX
	POP	AX
	RET
	;DS:SI-NEW ADRESS
ENDP
SET_UP_ANALIZER	PROC	NEAR
	;BL-ANALIZER FLAGS
	;DS:SI-EXIT ADRESSES FOR A NEW INT 1
	;ES:DI-ADRESS OF VERYFIING SEQUANCE OF A DUMPING PROGRAM
	;AX-NUMBER OF VERYFIING BYTES
	;CX:DX-NEW STACK FOR AN INT 1
	MOV	BP,SP
	PUSH	AX
	PUSH	BX
	PUSH	CX
	PUSH	DX
	PUSH	SI
	PUSH	DI
	PUSH	DS
	PUSH	ES

	MOV	CS:[AN_FLAGS_],BL
	MOV	CS:[AN_NEW_SP_],DX;SETUP NEW STACK POINTER
	MOV	CS:[AN_NEW_SS_],CX
	MOV	CS:[LEAVE_ADRESS_OFF_],SI;SETUP EXIT ADRESSES FOR A NEW INT 1
	MOV	CS:[LEAVE_ADRESS_SEG_],DS
	TEST	BL,10000000B
	JZ	NO_DUMP
	MOV	CS:[COMMAND_ADRESS_OFF_],DI;SETUP VERIFYING SEQUANCES OF A DUMP PROGRAM
	MOV	CS:[COMMAND_ADRESS_seg_],ES
	MOV	CS:[COMMAND_CHART_],AX;NUMBER OF VERIFIING BYTES
NO_DUMP:
	TEST	BL,01000000B
	JZ	NO_BEGIN_ADRESSES
	PUSH	BX
	MOV	ES,SS:[BP+2+4]
	MOV	BX,SS:[BP+4+4]
	CALL	CALCULATE_ABS_ADRESS
	MOV	CS:[ABS_ADRESS_H_BEGIN_],CL
	MOV	CS:[ABS_ADRESS_L_BEGIN_],AX
	POP	BX
NO_BEGIN_ADRESSES:
	TEST	BL,00100000B
	JZ	NO_END_ADRESSES
	PUSH	BX
	MOV	ES,SS:[BP+2]
	MOV	BX,SS:[BP+4]
	CALL	CALCULATE_ABS_ADRESS
	MOV	CS:[ABS_ADRESS_H_END_],CL
	MOV	CS:[ABS_ADRESS_L_END_],AX
	POP	BX
NO_END_ADRESSES:
	LEA	BP,SOURCE_L_
	TEST	BL,00010000B
	JZ	SOURCE_NOT
	XOR	AX,AX
	TEST	BL,00000010B
	JZ	SOURCE_0
	DEC	AX
SOURCE_0:
	MOV	CS:[BP],AX
	MOV	CS:[BP+2],AL
SOURCE_NOT:
	LEA	BP,DESTINATION_L_
	XOR	AX,AX
	TEST	BL,00001000B
	JZ	DESTINATION_NOT
	XOR	AX,AX
	TEST	BL,00000010B
	JZ	DESTINATION_0
	DEC	AX
DESTINATION_0:
	MOV	CS:[BP],AX
	MOV	CS:[BP+2],AL
DESTINATION_NOT:
	LEA	BP,STACK_L_
	XOR	AX,AX
	TEST	BL,00000100B
	JZ	STACK_NOT
	XOR	AX,AX
	TEST	BL,00000010B
	JZ	STACK_0
	DEC	AX
STACK_0:
	MOV	CS:[BP],AX
	MOV	CS:[BP+2],AL
STACK_NOT:
	TEST	BL,00000001B
	JZ	BX_NOT
	XOR	AX,AX
	TEST	BL,00000010B
	JZ	BX_0
	DEC	AX
BX_0:
	MOV	CS:[AN_BX_],AX
BX_NOT:
	POP	ES
	POP	DS
	POP	DI
	POP	SI
	POP	DX
	POP	CX
	POP	BX
	POP	AX
	RET
ENDP